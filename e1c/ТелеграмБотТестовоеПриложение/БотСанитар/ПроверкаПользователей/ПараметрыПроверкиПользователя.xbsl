импорт e1c::ТелеграмБот::Основное

@ВПодсистеме
статический метод Получить(Пользователь: УчетныеЗаписиТелеграм.Ссылка, Чат: Чаты.Ссылка): ПараметрыПроверкиПользователя

    пер Статус = СтатусПроверкиПользователя.НеВыполнялась
    пер ОжидаемыйОтвет = ""
    
    если не ПользовательТребуетПроверки(Пользователь)
        Статус = СтатусПроверкиПользователя.НеТребуется
    иначе если ЭтоПроверенныйПользователь(Пользователь)
        Статус = СтатусПроверкиПользователя.Выполнена
    иначе
        знч АктивнаяПроверка = АктивнаяПроверка(Пользователь, Чат)
        если АктивнаяПроверка.Активна
            Статус = СтатусПроверкиПользователя.Выполняется
            ОжидаемыйОтвет = АктивнаяПроверка.ОжидаемыйОтвет
        иначе
            Статус = СтатусПроверкиПользователя.НеВыполнялась
        ;
    ;
    
    возврат новый ПараметрыПроверкиПользователя(Пользователь, Чат, Статус, ОжидаемыйОтвет)

;

@Локально
статический метод ЭтоПроверенныйПользователь(Пользователь: УчетныеЗаписиТелеграм.Ссылка): Булево

    знч Запрос = Запрос{
        ВЫБРАТЬ
            УчетнаяЗапись
        ИЗ
            ПроверенныеПользователи
        ГДЕ
            УчетнаяЗапись == %Пользователь
    }
    
    исп РезультатЗапроса = Запрос.Выполнить()

    если не РезультатЗапроса.Пусто()
        возврат Истина
    ;

    возврат Ложь
    
;

@Локально
статический метод ПользовательТребуетПроверки(Автор: УчетныеЗаписиТелеграм.Ссылка): Булево
    знч Запрос = Запрос{
        ВЫБРАТЬ
            НЕ ЭтоБот КАК ПроверитьПользователя
        ИЗ
            УчетныеЗаписиТелеграм
        ГДЕ
            Ссылка == %Автор
    }

    исп РезультатЗапроса = Запрос.Выполнить()

    возврат РезультатЗапроса.ЕдинственныйИлиНеопределено()?.ПроверитьПользователя ?? Ложь
;

@Локально
структура ПараметрыАктивнойПроверки
    обз знч Пользователь: УчетныеЗаписиТелеграм.Ссылка
    обз знч Чат: Чаты.Ссылка
    знч ОжидаемыйОтвет: Строка
    знч Активна: Булево
;

@Локально
статический метод АктивнаяПроверка(Пользователь: УчетныеЗаписиТелеграм.Ссылка, Чат: Чаты.Ссылка): ПараметрыАктивнойПроверки
    
    знч Запрос = Запрос{
        ВЫБРАТЬ
            ОжидаемыйОтвет
        ИЗ
            АктивныеПроверки
        ГДЕ
            УчетнаяЗапись == %Пользователь
            И Чат == %Чат}

    исп РезультатЗапроса = Запрос.Выполнить()
    
    пер ОжидаемыйОтвет = ""
    пер Активна = Ложь
    
    если не РезультатЗапроса.Пусто()
        знч СостояниеПроверки = РезультатЗапроса.Единственный()
        ОжидаемыйОтвет = СостояниеПроверки.ОжидаемыйОтвет
        Активна = Истина
    ;

    возврат новый ПараметрыАктивнойПроверки(Пользователь, Чат, ОжидаемыйОтвет, Активна)
    
;